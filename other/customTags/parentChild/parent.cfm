<cfprocessingdirective suppresswhitespace="true">
<cfsetting enablecfoutputonly="true">
<cfif not thistag.hasEndTag>
	<cfoutput>&lt;cf_parent> requires a closing tag.</cfoutput>
	<cfexit>
</cfif>


<cfif thistag.executionMode eq "start">
	<!--- in the START section, validate any incoming variables, but don't do much else --->
	<cfparam name="attributes.birthdate" type="date">
	<cfparam name="attributes.name" type="string">

	<cfset iAge = dateDiff("yyyy", attributes.birthDate, now())>
<cfelse><!--- it's the END tag --->
	<!--- Do all the processing here, because we have now captured whatever output/processing requirements there were between the opening and closing tags --->

	<!--- restricting output here because custom tags output all their whitespace unless it's controlled --->
	<cfoutput><h2>#attributes.name#</h2></cfoutput>

	<!--- thistag.childData has been passed back by the child tags.  So the length of the array is how many children there are --->
	<cfoutput><h3>Summary</h3><p>#attributes.name# is #iAge# and has #arrayLen(thistag.childData)# children: </cfoutput>
	<cfloop index="i" from="1" to="#arrayLen(thistag.childData)#">
		<cfoutput><cfif i gt 1>, </cfif>#thistag.childData[i].name# (#thistag.childData[i].age#)</cfoutput>
	</cfloop>
	<cfoutput></p></cfoutput>
	<cfoutput><h3>Detail</h3></cfoutput>
	<cfoutput>#thisTag.generatedContent#</cfoutput><!--- this is actually generated by the child tags in this case --->
	<cfset thisTag.generatedContent = ""><!--- need to clear whatever is there out, because we've replaced it with different output--->
</cfif>
</cfprocessingdirective>